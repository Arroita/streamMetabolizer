model {
  
  # Convert daily rates to per-observation rates
  for(i in 1:n) {
    GPP[i] <- GPP.daily * frac.GPP[i] / depth[i]
    ER[i] <- ER.daily * frac.ER[i] / depth[i]
    K[i] <- K600.daily * KO2.conv[i] * frac.D[i]
  }
  
  # Model DO changes as in Hotchkiss & Hall 2014, eq. 2
  DO.mod[1] <- DO.obs[1]
  for(i in 2:n) {
    DO.mod[i] <- DO.mod[i-1] +
      GPP[i] + 
      ER[i] + 
      K[i] * (DO.sat[i] - DO.mod[i-1])
  } 
  
  # Compare all the DO predictions to their observations
  for (i in 2:n) {
    DO.obs[i] ~ dnorm(DO.mod[i], DO.err.tau)
  }
  
  # Prior on the sd of the errors between modeled and observed DO
  DO.err.tau ~ dgamma(0.001,0.001)
  
  # Daily mean values of GPP and ER (gO2 m^-2 d^-1) and K600 (m d^-1). 
  GPP.daily ~ dnorm(GPP.daily.mu, GPP.daily.tau)
  ER.daily ~ dnorm(ER.daily.mu, ER.daily.tau)
  K600.daily ~ dnorm(K600.daily.mu, K600.daily.tau)
  
}

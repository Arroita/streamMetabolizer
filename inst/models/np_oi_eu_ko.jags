# np_oi_eu_ko.jags

data {

  for(i in 1:n) {
    DO_obs_copy[i] <- DO_obs[i]
  }
  
}

model {

  # Coefficients by lag (e.g., frac_GPP[i] applies to the DO step from i to i+1)
  for(i in 1:(n-1)) {
    coef_GPP[i]  <- frac_GPP[i] / depth[i]
    coef_ER[i]   <- frac_ER[ i] / depth[i]
    coef_K600_full[i] <- KO2_conv[i] * frac_D[i] * 
      (DO_sat[i] - DO_obs_copy[i])
  }
  
  # Model DO time series (Euler version)
  DO_mod[1] <- DO_obs_copy[1]
  for(i in 1:(n-1)) {
    DO_mod[i+1] <- (
      DO_mod[i] +
      GPP_daily * coef_GPP[i] +
      ER_daily * coef_ER[i] +
      K600_daily * coef_K600_full[i]
    )
  }
  
  # Priors on observation error
  for(i in 1:n) {
    DO_obs[i] ~ dnorm(DO_mod[i], pow(err_obs_iid_sigma, -2))
  }
  err_obs_iid_sigma ~ dunif(err_obs_iid_sigma_min, err_obs_iid_sigma_max)
  
  # Priors on daily metabolism values
  GPP_daily ~ dnorm(GPP_daily_mu, pow(GPP_daily_sigma, -2))
  ER_daily ~ dnorm(ER_daily_mu, pow(ER_daily_sigma, -2))
  K600_daily ~ dnorm(K600_daily_mu, pow(K600_daily_sigma, -2))
  
}
